#!/usr/bin/awk -f

# chanfile-filter.awk -- Filter channel metadata files for EEW systems.
#
# Usage: ./chanfile-filter.awk filterfile.dat chanfile.dat > newchanfile.dat

# 0. Add contents of filter file to the `filter' hash.

# (Do this until the total number of records seen so far (NR) doesn't
#  match the record number in the current file (FNR).)
NR == FNR {
        # Don't include comment lines in the hash.
	if ($0 !~ /^#/) {
		# Keep track of how many filter entries with the `INDEX'
		# variable.
		filter[++INDEX] = $0
	}
	next
}

# 1. Don't print lines containing all four fields in the filter
#    records.

# (Only filter if it's not a comment line.)
$1 !~ /^#/ {
	# Loop over all the filter records.
	for (i = 1; i <= INDEX; i++) {
		# Test all four filter fields.
		split(filter[i], f)
		if ($1 == f[1] && $2 == f[2] && $3 == f[3] && $4 == f[4]) {
			# Keep a tally of deleted lines for modifying the
			# `rows returned' line.
			DELETED++
			next
		}
	}
	print $0
}

# 2. Pass comments.

$1 ~ /^#/ {
	# Decrement `rows returned' value.
	if ($0 ~ /rows returned/) {
		ROWS = $2 - DELETED
		print $1, ROWS, $3, $4
	}
	else if ($0 ~ /Generated by/) {
		print
		# Add a "Modified by" line.
		print "# Modified by", ARGV[0], "on", strftime()
	}
	else { print }
}
